buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '0.0.1'
group = 'io.github.ramboxeu'
archivesBaseName = 'chainmail'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

sourceSets {
    locator {
        java {
            srcDir 'src/locator/java'
        }

        resources {
            include '**/**'
        }

        compileClasspath += sourceSets.main.compileClasspath
    }

    main {
        java {
            srcDir 'src/main/java'
        }

        resources {
            include '**/**'
        }

        runtimeClasspath += sourceSets.locator.output
    }
}

minecraft {
    mappings channel: 'snapshot', version: '20200916-1.16.2'
    
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                chainmail {
                    sources sourceSets.main, sourceSets.locator
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                chainmail {
                    sources sourceSets.main, sourceSets.locator
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.3-34.1.0'
}

task langProviderJar(type: Jar) {
    baseName = 'chainmail-lang'

    from sourceSets.main.output

    manifest {
        attributes([
                'FMLModType': 'LANGPROVIDER',
                'Specification-Title': 'Mod Language Provider',
                'Specification-Vendor': 'Forge Development LLC',
                'Specification-Version': '1',
                'Implementation-Title': 'Chainmail',
                'Implementation-Version': "$version",
                'Implementation-Vendor': 'Chainmail'
        ])
    }
}

task locatorJar(type: Jar) {
    from sourceSets.locator.output

    manifest {
        attributes([
                "Implementation-Version": "$version"
        ])
    }
}

task publishJar(type: Jar) {
    from langProviderJar
    with locatorJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact publishJar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}